SELECT DISTINCT
  ?platformLabel
  ?property # we include the property as we can use it to link directly to the relevant statement in Wikidata
  ?url
  ?verified
  (?value AS ?account)
WHERE {
  ?property wdt:P31 wd:Q105388954 ;
            wikibase:statementProperty ?propValuePredicate ;
            wikibase:claim ?wdtClaimPredicate  ;
            wdt:P1630 ?formatter ;
            wdt:P9073 ?platform .

  wd:{{.}} ?wdtClaimPredicate ?statement .
  ?statement ?propValuePredicate ?value .

  OPTIONAL { ?statement pq:P1552 ?quality }
  MINUS { ?statement pq:P582 [] }
  BIND(IF(BOUND(?quality), IF(?quality = wd:Q28378282, True, False), False) AS ?verified)

  BIND(IRI(REPLACE(?formatter, '\\$1', ?value)) AS ?url)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
}
# we order by property to ensure a consistent order, we use the URI as its incremental ID is the best indication of "importance" we have
ORDER BY ?property
