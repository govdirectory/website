SELECT DISTINCT
  ?platformLabel
  ?property # we include the property as we can use it to link directly to the relevant statement in Wikidata
  ?url
  ?verified
WHERE {
  ?property wdt:P31 wd:Q105388954 ;
            wikibase:statementProperty ?propValuePredicate ;
            wikibase:claim ?wdtClaimPredicate  ;
            wdt:P1630 ?formatter ;
            p:P1647 ?subPropertyStatement .

  ?subPropertyStatement pq:P642 ?platform .

  wd:{{.}} ?wdtClaimPredicate ?statement .
  ?statement ?propValuePredicate ?value .

  OPTIONAL { ?statement pq:P1552 ?quality }

  BIND(IF(BOUND(?quality), IF(?quality = wd:Q28378282, True, False), False) AS ?verified)

  BIND(IRI(CONCAT(SUBSTR(?formatter, 1, STRLEN(?formatter) -2), ?value)) AS ?url)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" }
}
# we order by property to ensure a consistent order, we use the URI as its incremental ID is the best indication of "importance" we have
ORDER BY ?property