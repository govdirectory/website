SELECT DISTINCT
  ?platformLabel
  ?property # we include the property as we can use it to link directly to the relevant statement in Wikidata
  ?url
  (?value AS ?account)
WHERE {
  ?property wdt:P31 wd:Q105388954 ;
            wikibase:statementProperty ?propValuePredicate ;
            wikibase:claim ?wdtClaimPredicate  ;
            wdt:P1630 ?formatter ;
            wdt:P9073 ?platform .

  MINUS { ?property wdt:P31 wd:Q60457486 }
  MINUS { ?property wdt:P11245 [] } # Temporary workaround until modeling on Wikidata is fixed

  wd:{{.}} ?wdtClaimPredicate ?statement .
  ?statement ?propValuePredicate ?value .

  MINUS { ?statement pq:P582 [] }
  MINUS { ?statement wikibase:rank wikibase:DeprecatedRank }

  BIND(IRI(REPLACE(?formatter, '\\$1', ?value)) AS ?url)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,mul" }
}
# we order by property to ensure a consistent order, we use the URI as its incremental ID is the best indication of "importance" we have
ORDER BY ?property
