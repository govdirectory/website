# expected_result_count: 2208
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q40 AS ?country)

  VALUES ?type {
   wd:Q18683469 # Austrian government federal ministry (Bundesministerium) (13)
   wd:Q261543 # Austrian federated states (Bundel√§nder) (9)
   wd:Q871419 #Austrian regional districts (94)
   wd:Q667509 # municipality of Austria (2092)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P31 wd:Q19953632 }
  MINUS { ?org wdt:P31 wd:Q15893266 }
  MINUS { ?org p:P31 ?p.
          ?p ps:P31 wd:Q667509;
             pq:P582 [] .
          MINUS { ?org p:P31 ?s.
                  ?s ps:P31 wd:Q667509;
                     pq:P580 ?start .
                  FILTER NOT EXISTS { ?s pq:P582 []}
                }
        }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
