# expected_result_count: 36
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
WHERE {
  BIND(wd:Q241 AS ?country)

  VALUES ?type {
    wd:Q132215848 # Ministries (21)
    wd:Q115291 # Provinces (15)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .
  
  MINUS { ?org p:P31 ?s . ?s pq:P582 [] } # exclude some former provinces
  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,es,mul" }
}
ORDER BY ?type ?orgLabel
