SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q35 AS ?country)
  VALUES ?type {
    wd:Q62326 # Region of Denmark (5)
    wd:Q2177636 # Municipality of Denmark (98)
    wd:Q110636698 # Ministries (18)
  }
  ?org wdt:P31 ?type ;
       wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da". }
}
ORDER BY ?type ?topic
