# expected_result_count: 124
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q35 AS ?country)
  VALUES ?type {
    wd:Q62326 # Region of Denmark (5)
    wd:Q2177636 # Municipality of Denmark (98)
    wd:Q110636698 # Ministries (21)
  }
  ?org wdt:P31 ?type ;
       wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] MINUS { ?org p:P1366 ?s . ?s pq:P585 ?replaced . FILTER (?replaced > NOW() )  } }
  MINUS { ?org wdt:P3999 [] }
  MINUS { ?org wdt:P571 ?start . FILTER (?start > NOW() ) }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,da,mul". }
}
ORDER BY ?type ?topic
