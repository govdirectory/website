# expected_result_count: 340
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
WHERE {
  BIND(wd:Q33 AS ?country)

  VALUES ?type {
    wd:Q193512 # Region (19)
    wd:Q10495402 # Ministries (12)
    wd:Q856076 # Municipalities (309)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,fi,sv,mul" }
}
ORDER BY ?type ?orgLabel