# expected_result_count: 263
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q183 AS ?country)
{
    # agencies with country set to Germany
  VALUES ?type {
    wd:Q1221156 # state of Germany (16)
    wd:Q896375 # federal ministry (16)
    wd:Q22721 # governmental districts (10)
    wd:Q829277 # governmental districts of Nordrhein-Westfalen (5)
    wd:Q42612577 # governmental districts of Baden-WÃ¼rttemberg (4)
  }

  ?org wdt:P31 ?type ;
       wdt:P17 ?country .
} UNION {
    # German agencies which does not necessarily have country set to Germany
    VALUES ?type {
      wd:Q3917681 # embassies (154)
      wd:Q372690 # consulate generals (49)
      wd:Q7843791 # consulats (7)
      wd:Q5244910 # de facto embassies (2)
    }

    ?org wdt:P31 ?type; wdt:P137 ?country .
  }
  MINUS { ?org wdt:P576 [] MINUS { ?org wdt:P31 wd:Q3917681 } } # To get the embassy in Yemen
  MINUS { ?org wdt:P1366 [] MINUS { ?org wdt:P5817 wd:Q55654238 } } # To get the embassy in Belgrade
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,mul". }
}
ORDER BY ?type ?topic
