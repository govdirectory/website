# expected_result_count: 95
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q211 AS ?country)
  {
    VALUES ?type {
      wd:Q3345345 # Municipality of Latvia (35)
      wd:Q131579945 # ministry of Latvia (14)
    }
    ?org wdt:P31 ?type ;
         wdt:P17 ?country .
  } UNION {
    VALUES ?type {
      wd:Q3917681 # embassy (38)
      wd:Q2360219 # permanent mission (7)
      wd:Q7843791 # consulate (1)
    }
    ?org wdt:P31 ?type ;
         wdt:P137 ?country .
  }
  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,lv,mul". }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?topic
