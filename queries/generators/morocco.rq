# expected_result_count: 431
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q1028 AS ?country)
{ 
  VALUES ?type {
    wd:Q20390905 # ministries (19)
    wd:Q121614922 # public institutions (228)
    wd:Q119811662   # regional councils (12)
    wd:Q15629582 # provinces (62)
    wd:Q15623950 # prefecture (13)
  }
  ?org wdt:P31 ?type .
  ?org wdt:P17 ?country .
} UNION {
  VALUES ?type { 
    wd:Q3917681 # embassies (97) 
  }
  ?org wdt:P31 ?type .
  ?org wdt:P137 ?country .
}
  
  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }
  MINUS { ?org wdt:P31 wd:Q19953632. }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,fr,ar,zgh,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
