# expected_result_count: 417
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
WHERE {
  BIND(wd:Q55 AS ?country)

  {
  # agencies with country set to the Netherlands
  VALUES ?type {
    wd:Q3143387 # ministries (15)
    wd:Q4500821 # appeals court (4)
    wd:Q190752  # supreme court (2)
    wd:Q2125157 # district court (11)
    wd:Q134390  # provinces (12)
    wd:Q702081 # water boards (21)
    wd:Q2039348 # municipalities (342)
    wd:Q26934845 # integral overseas territory (3)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .
  } UNION {
    # special case without a unique type
    VALUES ?org {
      wd:Q1857094 # cabinet of the King (1)
    }

    BIND("cabinet of the King" AS ?typeLabel)
  }
  UNION {
    ?org wdt:P31 wd:Q947233 . # high council of the state (6)
    BIND("High Council of State" AS ?typeLabel)
  }

  MINUS { ?org wdt:P576 [] . MINUS { ?item rdf:type wdno:P576 } }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P582 [] .
          MINUS { ?org wdt:P582 ?ddate ;
                       wdt:P580 ?idate .
                  FILTER ( ?idate > ?ddate )
               }
        }
  MINUS { 
    ?org p:P31 ?inStatement .
    ?inStatement pq:P582 [] .
    MINUS { ?org p:P31 ?p.
            ?p ps:P31 wd:Q2039348; pq:P580 ?start .
            FILTER NOT EXISTS { ?p pq:P582 []}
    }             
  }
  MINUS { ?org p:P31 ?p.
            ?p ps:P31 wd:Q2039348; pq:P580 ?start .
            FILTER ( ?start > NOW() )
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,nl,mul" }
}
ORDER BY ?type ?orgLabel
