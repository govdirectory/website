# expected_result_count: 174
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q664 AS ?country)

    # agencies with country set to New Zealand
    VALUES ?type {
      wd:Q116973815 # public service departments (32)
      wd:Q130137120 # departemental agencies (7)
      wd:Q8041036 # wƒÅnanga (3)
      wd:Q130420294 # state-owned enterprise of New Zealand (12)
      wd:Q65600637 # regional council of New Zealand (11)
      wd:Q941036 # territorial authority of New Zealand (67)
      wd:Q125959397 # Autonomous Crown Entity (16)
      wd:Q126041101 # Independent Crown Entity (19)
      wd:Q1141791 # Crown Research Institute (7)     
    }

    ?org wdt:P31 ?type; wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,mi" }
  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

} ORDER BY ?typeLabel
