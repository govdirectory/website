# expected_result_count: 595
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
WHERE {
  BIND(wd:Q20 AS ?country)

  {
    VALUES ?type {
      wd:Q755707 # Municipality of Norway (357)
      wd:Q109644271 # ministry of Norway (17)
      wd:Q16973854 # district court (28)
      wd:Q101244256 # directorates (72)
      wd:Q11979280 # land consolidation court (19)
      wd:Q1769285 # corts of appeals (6)
      wd:Q5177805 # county municipalities (14)
    }

    ?org wdt:P31 ?type ;
         wdt:P17 ?country .
  } UNION {
    VALUES ?type {
      wd:Q3917681 # embassy (82)
    }
    ?org wdt:P31 wd:Q3917681 ;
         wdt:P137 ?country .
  }

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] .
          MINUS { ?org p:P1366 ?s. ?s pq:P582 [] }
          MINUS { ?org p:P1366 ?t . ?t pq:P585 ?time . FILTER (?time > NOW()) }
        }
  MINUS { ?org wdt:P3999 [] }
  MINUS { ?org wdt:P571 ?incept . FILTER (?incept > NOW()) }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,nb,no,nn,mul". }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?topic
