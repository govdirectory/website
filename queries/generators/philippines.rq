# expected_result_count: 1764
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q928 AS ?country)

  VALUES ?type {
    wd:Q111188397 # executive department (22)
    wd:Q24698 # region (18)
    wd:Q24746  # province (82)
    wd:Q106078286 # component city (111)
    wd:Q29946056 # highly urbanized city (33)
    wd:Q106079704 # independent component city (5)
    wd:Q24764 # Philippine municipality (1493)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org p:P1366 ?r . MINUS { ?r pq:P582 [] } }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,tl,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
