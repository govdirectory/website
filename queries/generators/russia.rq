# expected_result_count: 22
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q159 AS ?country)

  VALUES ?type {
    wd:Q4481741 # ministries (21)
    wd:Q7188 # government (1)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P576 ?ddate .
         MINUS { ?org wdt:P31 wd:Q4481741 ;
                       wdt:P576 ?ddate ;
                       wdt:P571 ?idate .
                FILTER ( ?idate > ?ddate )
               }
         }          

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,ru,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
