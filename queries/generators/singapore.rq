# expected_result_count: 41
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q334 AS ?country)

  VALUES ?type {
    wd:Q35749 # Parliament of Singapore (1)
    wd:Q1320217 # Government of Singapore (1)
    wd:Q190752 # Supreme Court of Singapore (1)
    wd:Q112818479 # Ministries (16)
    wd:Q3507268 # Town Councils (17)
    wd:Q4426538  # Community Development Council Districts (5)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,tl,ms,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
