# expected_result_count: 768
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q34 AS ?country)

  {
    # agencies with country set to Sweden
    VALUES ?type {
      wd:Q68295960 # Swedish government agency (247) ✔
      wd:Q107407151 # Swedish government agency under the parliament (12) ✔
      wd:Q127448 # municipality of Sweden (290) ✔
      wd:Q1754161 # regional council in Sweden (20) ✔
      wd:Q10397683 # AP-fund (6) ✔
      wd:Q16296141 # state-owned enterprise (3) ✔
      wd:Q10330441 # state level departments (10) ✔
      wd:Q341627 # court of appeal (6) ✔
      wd:Q2065704 # tingsrätt (48) ✔
      wd:Q190752 # supreme court (1) ✔
      wd:Q135399035 # administrative supreme court (1) ✔
      wd:Q1289455 # administrative court of appeal (4) ✔
      wd:Q18292311 # migration court (4) ✔
      wd:Q10530889 # regional rent tribunal (8) ✔
    }

    ?org wdt:P31 ?type; wdt:P17 ?country .
  } UNION {
    # Swedish agencies which does not necessarily have country set to Sweden
    VALUES ?type {
      wd:Q108059166 # delegations (7) ✔
      wd:Q108058047 # general consulates (9) ✔
      wd:Q3917681 # embassies (91) ✔
    }

    ?org wdt:P31 ?type; wdt:P137 ?country .
  } UNION {
    # special case without a unique type
    VALUES ?org {
      wd:Q10475844 # domstolsverket (1) ✔
    }

    BIND("courts administration agency" AS ?typeLabel)
  }

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] . MINUS { ?org wdt:P31 wd:Q127448 } } # Explict exception for municipalities due to their modeling
  MINUS { ?org wdt:P3999 [] }

  OPTIONAL {
    ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,sv,mul" }
  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

} ORDER BY ?typeLabel
