# expected_result_count: 2154
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q39 AS ?country)

  VALUES ?type {
    wd:Q113015553 # federal ministry of switzerland (7)
    wd:Q23058 # canton of switzerland (26)
    wd:Q70208 # municipality of Switzerland (2121)
  }
  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .

  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }
  MINUS { ?org wdt:P582 [] }
  MINUS { ?org wdt:P571 ?date . FILTER (DATATYPE(?date) = xsd:dateTime && ?date > now()) }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,de,fr,it,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
