# expected_result_count: 232
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  ?cofog
WHERE {
  BIND(wd:Q145 AS ?country)
  {
	  VALUES ?type {
	  wd:Q21451686 # Scottish local authorities (32)
	  wd:Q83425186 # Scottish NHS boards (14)
	  wd:Q20667323 # Scottish Health and social care partnerships (31)
	  wd:Q108838260 # Scottish executive agencies (10)
      wd:Q137322982 # Scottish executive non-departmental public bodies (46)
      wd:Q137324408 # Scottish advisory non-departmental public bodies (9)
      wd:Q137325036 # Scottish non-ministerial offices (10)
      wd:Q137366971 # Scottish public corporations (4)
	  }
      ?org wdt:P31 ?type ;
           wdt:P17 ?country .
  }
  UNION
  { # More Scottish organizations
		VALUES ?type {
			wd:Q55657615 wd:Q524778 wd:Q169180 # commissioners and ombudsmen (7)
            wd:Q7139410 # parole board (1)
            wd:Q1412224 # tribunal (10)
            wd:Q16931297 # King's printer (1)
            wd:Q7188 # government (Scotland) (1)
		}
		?org wdt:P31 | wdt:P1454 ?type ;
	       wdt:P1001 wd:Q22 .
		MINUS { ?org wdt:P31 wd:Q5532360 }
        MINUS { ?org wdt:P1001 wd:Q1156248 }
        MINUS { ?org p:P1001 ?js. ?js pq:P582 [] }
	}
	UNION
	{ # Scottish courts (56)
     VALUES ?type {
       wd:Q109768640 # Sheriff district court (39)
       wd:Q16988501 # Justice of the peace court (6)
       wd:Q190752 # Supreme court (1)
       wd:Q4959031 # Appelate court (1)
       wd:Q7494979 # Sheriff court (6)
       wd:Q41487 # Court (3)
    }
	  ?org wdt:P361 wd:Q4993654 ;
         wdt:P31 ?type .
	}
  MINUS { ?org wdt:P576 [] }
  OPTIONAL { ?org wdt:P9798/wdt:P279* ?cofog .
    MINUS { ?cofog wdt:P279 [] }
  }
  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,mul,sco,gd" }
}ORDER BY ?type ?orgLabel
