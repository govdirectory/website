# expected_result_count: 33
SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  ?country
  (GROUP_CONCAT(DISTINCT ?fieldOfWork; separator=",") AS ?fieldOfWorkList)
WHERE {
  BIND(wd:Q77 AS ?country)
  VALUES ?type { 
    wd:Q134301525 # ministries (14)
    wd:Q56059 # department (19) 
  }
  ?org wdt:P31 ?type .
  MINUS { ?org wdt:P576 [] }
  MINUS { ?org wdt:P1366 [] }

  OPTIONAL {
    ?org wdt:P101 ?fieldOfWork .
    # TODO: check for end date
  }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,es,mul" }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?type ?orgLabel
